{
  config,
  lib,
  pkgs,
  _utils,
  _domain_base,
  ...
}:
let
  secrets = _utils.setupSecrets config {
    secrets = [
      "mas-config"
    ];
    extra = {
      owner = "mas";
      group = "mas";
    };
  };

  dataDir = "/storage/matrix-authentication-service";
  settingsFile = "${dataDir}/settings.yaml";

  port = 8089;
  port2 = 8083;
in
{
  imports = [
    secrets.generate
  ];

  users = {
    groups.mas = { };
    users.mas = {
      isSystemUser = true;
      group = "mas";
      home = dataDir;
      createHome = true;
      description = "Matrix Authentication Service";
    };
  };

  systemd.services.matrix-authentication-service = {
    enable = true;
    description = "Matrix Authentication Service";
    wantedBy = [ "multi-user.target" ];
    after = [ "postgresql.service" ];
    requires = [ "postgresql.service" ];
    serviceConfig = {
      Restart = "on-failure";
      EnvironmentFile = secrets.get "mas-config";
      ExecStart = "${pkgs.matrix-authentication-service}/bin/mas-cli -c ${settingsFile} server";
      # DynamicUser = true;
      User = "mas";
      Group = "mas";
      WorkingDirectory = "${dataDir}";
    };
    preStart = ''
      ${pkgs.coreutils}/bin/mkdir -p '${dataDir}'
      test -f '${settingsFile}' && ${pkgs.coreutils}/bin/rm -f '${settingsFile}'
      ${pkgs.envsubst}/bin/envsubst \
        -o '${settingsFile}' \
        -i '${
          (pkgs.writeText "mas-settings.yaml" (
            lib.generators.toYAML { } {
              http = {
                listeners = [
                  {
                    name = "web";
                    resources = [
                      { name = "discovery"; }
                      { name = "human"; }
                      { name = "oauth"; }
                      { name = "compat"; }
                      { name = "graphql"; }
                      { name = "assets"; }
                    ];
                    binds = [
                      { address = "[::]:${toString port}"; }
                    ];
                    proxy_protocol = false;
                  }
                  {
                    name = "internal";
                    resources = [
                      { name = "health"; }
                    ];
                    binds = [
                      {
                        host = "localhost";
                        port = port2;
                      }
                    ];
                    proxy_protocol = false;
                  }
                ];
                trusted_proxies = [
                  "192.168.0.0/16"
                  "172.16.0.0/12"
                  "10.0.0.0/10"
                  "127.0.0.1/8"
                  "fd00::/8"
                  "::1/128"
                ];
                public_base = "https://mas.${_domain_base}/";
                issuer = "https://${_domain_base}/";
              };
              database = {
                uri = "postgresql://mas@localhost/mas?host=/run/postgresql";
              };
              email = {
                from = "\"Authentication Service\" <root@localhost>";
                reply_to = "\"Authentication Service\" <root@localhost>";
                transport = "blackhole";
              };
              secrets = {
                encryption = "$encryption";
                keys = [
                  {
                    key = "$key1";
                  }
                  {
                    key = "$key2";
                  }
                  {
                    key = "$key3";
                  }
                  {
                    key = "$key4";
                  }
                ];
              };
              passwords = {
                enabled = false;
              };
              matrix = {
                homeserver = _domain_base;
                endpoint = "http://[::1]:${toString port}/";
                secret = "$matrix_secret";
              };

              clients = [
                {
                  client_id = "0000000000000000000SYNAPSE";
                  client_auth_method = "client_secret_basic";
                  client_secret = "$client_secret";
                }
              ];

              upstream_oauth2 = {
                providers = [
                  {
                    id = "01KJKAM7BDPSYJDN4YXSZQYX1H";
                    human_name = "Authelia";
                    issuer = "https://auth.${_domain_base}";
                    client_id = "IkhbiLxn.MQVKQeBFAlvMfu3-RdUMScM0PcnpDSyjSTGwjs0VGveq_yii.GOavtNyoZYC9U6";
                    client_secret = "$provider_client_secret";
                    token_endpoint_auth_method = "client_secret_basic";
                    scope = "openid profile email";
                    discovery_mode = "insecure";
                    claims_imports = {
                      localpart = {
                        action = "require";
                        template = "{{ user.preferred_username }}";
                      };
                      displayname = {
                        action = "suggest";
                        template = "{{ user.name }}";
                      };
                      email = {
                        action = "suggest";
                        template = "{{ user.email }}";
                        set_email_verification = "always";
                      };
                    };
                  }
                ];
              };
            }
          ))
        }'
      ${pkgs.coreutils}/bin/chmod 600 '${settingsFile}'
    '';
  };
}
